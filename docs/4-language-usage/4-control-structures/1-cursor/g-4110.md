# G-4110: Always use %NOTFOUND instead of NOT %FOUND to check whether a cursor returned data.

!!! tip "Minor"
    Maintainability

## Reason

The readability of your code will be higher when you avoid negative sentences.

## Example (bad)

```sql
declare
   cursor c_employee is 
      select last_name
            ,first_name
        from employee
       where commission_pct is not null;
           
   r_employee  c_employee%rowtype;
begin
   open c_employee;
   
   <<read_employees>>
   loop
      fetch c_employee into r_employee;
      exit read_employees when not c_employee%found;
   end loop read_employees;
   
   close c_employee;
end;
/
```

## Example (good)

```sql
declare
   cursor c_employee is 
      select last_name
            ,first_name
        from employee
       where commission_pct is not null;
           
   r_employee  c_employee%rowtype;
begin
   open c_employee;
   
   <<read_employees>>
   loop
      fetch c_employee into r_employee;
      exit read_employees when c_employee%notfound;
   end loop read_employees;
   
   close c_employee;
end;
/
```
